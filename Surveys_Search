ALTER PROC [dbo].[Surveys_Search] 
                    @Query nvarchar(100)  
                    ,@pageIndex INT 
                    ,@pageSize  INT
AS

/*
	----TEST CODE----

	Declare   @Query nvarchar(100) = 'Ohio'
	          ,@pageIndex int = 0
			  ,@pageSize int = 10

	Declare @offset int = @pageSize * @pageIndex

	Execute dbo.Surveys_Search  @Query
								,@pageIndex
								, @pageSize


*/

    BEGIN
        DECLARE @offset INT= @pageIndex * @pageSize;
        SELECT s.Id 
               ,s.Name 
               ,s.Description 
               ,ss.Id as SurveyStatusId
			   ,ss.Name AS SurveyStatus 
			   ,s.SurveyTypeId
               ,st.Name AS SurveyType
			   ,s.TitleTypesId
			   ,tt.Name AS Titles
			   ,s.PrimaryImageUrl
               ,s.CreatedBy 
               ,s.ModifiedBy 
               ,s.DateCreated 
               ,s.DateModified 
			   ,(Select BP.[Id]
						,BP.[Name]
						from dbo.SurveyPlanTypes as SP
						JOIN dbo.BasicPlanTypes as BP
						on SP.BasicPlanTypesId=BP.Id
						where SP.SurveyId = s.Id
						For JSON Auto) As BasicPlanTypes
               ,TotalCount = COUNT(1) OVER()
        FROM dbo.Surveys AS s
             inner JOIN dbo.SurveyTypes AS st 
			 ON s.SurveyTypeId = st.Id
             inner JOIN dbo.SurveyStatus AS ss 
			 ON s.StatusId = ss.Id
			 inner JOIN dbo.TitleTypes AS tt
			 ON s.TitleTypesId = tt.Id
			 WHERE (s.Name LIKE '%' + @Query + '%')
        ORDER BY DateModified
        OFFSET @offset ROWS FETCH NEXT @pageSize ROWS ONLY
End;
